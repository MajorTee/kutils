buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}

plugins {
	id "com.jfrog.bintray" version "1.8.4"
	id "io.gitlab.arturbosch.detekt" version "1.0.0.RC8"
	id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'

apply plugin: 'io.gitlab.arturbosch.detekt'

group 'io.gitlab.arturbosch'
version '0.2.0'

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: uri('../repo'))
		}
	}
}

repositories {
	mavenCentral()
	mavenLocal()
}


detekt {
	version = "1.0.0.RC8"
	defaultProfile {
		input = "$project.projectDir"
		filters = ".*/resources/.*, .*/build/.*"
		config = "$project.projectDir/default-detekt-config.yml"
		baseline = "$project.projectDir/baseline.xml"
	}
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
	testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
	testCompile "junit:junit:4.12"
}

bintray {
	user = System.getenv("BINTRAY_USER") ?: ""
	key = System.getenv("BINTRAY_API_KEY") ?: ""
	publications = ["kutils"]

	pkg {
		repo = 'generic'
		name = 'kutils'
		userOrg = 'arturbosch'
		licenses = ['Apache-2.0']
		vcsUrl = "https://github.com/arturbosch/kutils"

		version {
			name = project.version
			released = new Date()
		}
	}
}

publishing {
	publications {
		kutils(MavenPublication) {
			from components.java
			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}
