buildscript {
	ext.kotlin_version = '1.1.3'

	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id "com.jfrog.bintray" version "1.7.3"
	id "io.gitlab.arturbosch.detekt" version "1.0.0.M12.1"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'

apply plugin: 'io.gitlab.arturbosch.detekt'

group 'io.gitlab.arturbosch'
version '0.1.6'

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: uri('../repo'))
		}
	}
}

repositories {
	mavenCentral()
	mavenLocal()
}


detekt {
	version = "1.0.0.M12.1"
	profile("main") {
		input = "$project.projectDir"
		filters = ".*/test/.*, .*/resources/.*, .*/build/.*"
		config = "$project.projectDir/default-detekt-config.yml"
	}
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
	testCompile "junit:junit:4.12"
}

bintray {
	user = System.getenv("BINTRAY_USER") ?: ""
	key = System.getenv("BINTRAY_API_KEY") ?: ""
	publications = ["kutils"]

	pkg {
		repo = 'generic'
		name = 'kutils'
		userOrg = 'arturbosch'
		licenses = ['Apache-2.0']
		vcsUrl = "https://github.com/arturbosch/kutils"

		version {
			name = project.version
			released = new Date()
		}
	}
}

publishing {
	publications {
		kutils(MavenPublication) {
			from components.java
			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}
